#!/usr/bin/env yarn repl -s

PrintTransactionLogs
Alias CompHolder "0x19bc62ff7cd9ffd6bdced9802ff718f09f7259f1"
Alias USDCWhale "0x826356424140c82938e7088abb103fb2ad0fa160"
Web3Fork "https://mainnet-eth.compound.finance/@10675233" (CompHolder USDCWhale)
UseConfigs mainnet

-- Deploy the flywheel impl

ComptrollerImpl Deploy ComptrollerG5

-- Propose to apply the patch

From CompHolder (Comp Delegate CompHolder)
From CompHolder (Governor GovernorAlpha Propose "Borrow Limit Comptroller Patch" [(Address Unitroller) (Address ComptrollerG5) (Address Unitroller)] [0 0 0] ["_setPendingImplementation(address)" "_become(address)" "_setBorrowLimitGuardian(address)"] [[(Address ComptrollerG5)] [(Address Unitroller)] [(Address CompHolder)]])

-- Vote for, queue, and execute the proposal

MineBlock
From CompHolder (Governor GovernorAlpha Proposal LastProposal Vote For)
AdvanceBlocks 20000
Governor GovernorAlpha Proposal LastProposal Queue
IncreaseTime 604910
Governor GovernorAlpha Proposal LastProposal Execute
ComptrollerImpl ComptrollerG5 MergeABI

Assert Equal (Address (Unitroller Implementation)) (Address ComptrollerG5)

From USDCWhale (Trx GasPrice 0 (Erc20 USDC Approve cUSDC UInt256Max))
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Mint 10000e6))
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Borrow 100e6))

From CompHolder (Comptroller SetMarketBorrowLimits (cUSDC) (10e6))

AllowFailures

From USDCWhale (Trx GasPrice 0 (CToken cUSDC Borrow 100e6))
Assert Revert

From USDCWhale (Trx GasPrice 0 (CToken cUSDC RepayBorrow 100e6))
From USDCWhale (Trx GasPrice 0 (CToken cUSDC Borrow 10e6))


Print "Borrow Limit Comptroller Patch OK!"